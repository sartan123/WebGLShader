<!DOCTYPE html>
<html>
    <head>
        <title>Demo</title>
        <script src="script.js"></script>
        <script src="minMatrix.js"></script>
        <script id="vs" type="x-shader/x-vertex">
            attribute vec3 position;
            attribute vec4 color;
            attribute vec3 normal;
            uniform mat4 mvpMatrix;
            varying vec4 vColor;
            varying vec3 vNormal;
            void main(){
                gl_Position =  vec4(position, 1.0);
                vColor = color;
                vNormal = normal;
            }
        </script>
        <script id="fs" type="x-shader/x-fragment">
            precision mediump float;
            uniform vec2 resolution;
            varying vec4 vColor;
            varying vec3 vNormal;
            struct Ray{
                vec3 position;
                vec3 direction;
            };
            struct Sphere{
                vec3 position;
                vec3 color;
                float radian;
            };
            struct Plane{
                vec3 position;
                vec3 normal;
                vec3 color;
            };
            struct Intersection{
                float distance;
                vec3 hitPoint;
                vec3 normal;
                vec3 color;
                float hit;
            };

            const vec3 lightDirection = vec3(0.0, 1.0, 1.0);

            void intersectSphere(Sphere s, Ray r, inout Intersection i){
                vec3 rs = r.position - s.position;
                float b = dot(rs, r.direction);
                float c = dot(rs, rs) - (s.radian * s.radian);
                float d = b*b - c;
                float t = -b - sqrt(d);
                if(d > 0.0 && t > 0.0 && t < i.distance){
                    if(t > 0.0){
                        i.hitPoint = r.position + r.direction * t;
                        i.normal = normalize(i.hitPoint - s.position);
                        float d = clamp(dot(lightDirection, i.normal), 0.1, 1.0);
                        i.color = s.color * d;
                        i.distance = t;
                        i.hit = 1.0;
                    }
                }
            }
            void intersectPlane(Plane p, Ray r, inout Intersection i){
                float d = -dot(p.position, p.normal);
                float v = dot(r.direction, p.normal);
                float t = -(dot(r.position, p.normal) + d) / v;
                if(t > 0.0 && t < i.distance){
                   i.hitPoint = r.position + r.direction * t;
                   i.normal = p.normal;
                   float d = clamp(dot(i.normal, lightDirection), 0.1, 1.0);
                   float m = mod(i.hitPoint.x, 2.0);
                   float n = mod(i.hitPoint.z, 2.0);
                   if((m > 1.0 && n > 1.0) || (m < 1.0 && n < 1.0)){
                       d *= 0.5;
                   }
                   float f = 1.0 - min(abs(i.hitPoint.z), 25.0) * 0.04;
                   i.color = p.color * d * f;
                   i.distance = t;
                   i.hit = 1.0;
                }
            }

            void main(){
                vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);

                Ray ray;
                ray.position = vec3(0.0, 0.0, 6.0);
                ray.direction = normalize(vec3(p.x, p.y, -1.0));


                Sphere sphere;
                sphere.position = vec3(-4.0, 0.0, -1.0);
                sphere.color = vec3(1.0, 0.0, 0.0);
                sphere.radian   = 1.0;

                Sphere sphere2;
                sphere2.position = vec3(0.0, 0.0, 0.0);
                sphere2.color = vec3(0.0, 1.0, 0.0);
                sphere2.radian   = 1.0;

                Sphere sphere3;
                sphere3.position = vec3(4.0, 0.0, -2.0);
                sphere3.color = vec3(0.0, 0.0, 1.0);
                sphere3.radian   = 1.0;

                Plane plane;
                plane.position = vec3(0.0, -1.0, 0.0);
                plane.normal = vec3(0.0, 1.0, 0.0);
                plane.color = vec3(1.0);

                Intersection i;
                i.hitPoint = vec3(0.0, 0.0, 0.0);
                i.normal = vec3(0.0, 0.0, 0.0);
                i.color = vec3(0.2, 0.3, 0.6);
                i.hit = 0.0;
                i.distance = 1.0e+30;
                
                intersectSphere(sphere, ray, i);
                intersectSphere(sphere2, ray, i);
                intersectSphere(sphere3, ray, i);
                intersectPlane(plane, ray, i);

                if(i.hit != 0.0)
                {
                    vec3 lightDir = normalize(vec3(1.0));
                    float eps = 0.0001;
                    Ray rsh;
                    rsh.position = i.hitPoint + i.normal * eps;
                    rsh.direction = reflect(ray.direction, i.normal);
                    Intersection ish;
                    ish.hit = 0.0;
                    ish.distance = 1.0e+30;
                    ish.normal = ish.color = vec3(0.0);

                    intersectSphere(sphere, rsh, ish);
                    intersectSphere(sphere2, rsh, ish);
                    intersectSphere(sphere3, rsh, ish);
                    intersectPlane(plane, rsh, ish);
                    
                    i.color = max(0.0, dot(i.normal, lightDir)) * i.color;
                    i.color += ish.color;
                }
                gl_FragColor = vec4(i.color, 1.0);
            }
        </script>
    </head>
    <body>
        <canvas id="canvas"></canvas>
    </body>
</html>